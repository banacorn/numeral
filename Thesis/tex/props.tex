\documentclass[../thesis.tex]{subfiles}
\begin{document}

\chapter{Properties of Natural Numbers and Equational Reasoning}\label{props}

Properties of natural numbers play a big role in the development of proofs in
this thesis. With propositional equality at our disposal, we will demonstrate how
to prove properties such as the commutative property of addition.
As proofs get more complicated, we will make proving easier by introducing a
powerful tool: \textit{equational reasoning}.

\paragraph{Remark}
Readers who are familiar with equational/preorder reasoning may skip this chapter.

\section{Proving Equational Propositions}

\paragraph{Right identity of addition}

Recap the definition of addition on {\lstinline|ℕ|}.

\begin{lstlisting}
_+_ : ℕ → ℕ → ℕ
zero  + y = y
suc x + y = suc (x + y)
\end{lstlisting}

{\lstinline|_+_|} is defined by induction on the first argument. That means we
get the \textit{left identity} of addition for free, as {\lstinline|zero + y|}
and {\lstinline|y|} are \textit{computationally equal}. However, this is not the
case for the \textit{right identity}. It has to be proven explicitly.

\begin{lstlisting}
+-right-identity : (n : ℕ) → n + 0 ≡ n
+-right-identity zero    = ?0
+-right-identity (suc n) = ?1
\end{lstlisting}

By induction on the only argument, we get two sub-goals:

\begin{lstlisting}
?0 : 0 ≡ 0
?1 : suc (n + 0) ≡ suc n
\end{lstlisting}

{\lstinline|?0|} can be trivially proven with {\lstinline|refl|}.
As for {\lstinline|?1|}, we see that its type looks a lot like the proposition
we are proving, except that both sides of the equation are ``coated'' with a {\lstinline|suc|}.
With {\lstinline|cong suc : ∀ {x y} → x ≡ y → suc x ≡ suc y|}, we could substitute
a term in {\lstinline|suc|} with another if they are equal, and finish
the proof by recursively calling itself with a \textit{smaller} argument.

\begin{lstlisting}
+-right-identity : ∀ n → n + 0 ≡ n
+-right-identity zero    = refl
+-right-identity (suc n) = cong suc (+-right-identity n)
\end{lstlisting}

\paragraph{Moving suc to the other side}

This is an essential lemma for proving more advanced theorems.
The proof also follows a similar pattern as that of {\lstinline|+-right-identity|}.
\footnote{In fact, all of these proofs (hence programs) can be generalized to
a \textit{fold}, but that is not the point here.}

\begin{lstlisting}
+-suc : ∀ m n → m + suc n ≡ suc (m + n)
+-suc zero    n = refl
+-suc (suc m) n = cong suc (+-suc m n)
\end{lstlisting}

\paragraph{Commutative property of addition}

Similarly, by induction on the first argument, we get two sub-goals:

\begin{lstlisting}
+-comm : ∀ m n → m + n ≡ n + m
+-comm zero    n = ?0
+-comm (suc m) n = ?1

?0 : n           ≡ m + zero
?1 : suc (m + n) ≡ m + suc n
\end{lstlisting}

{\lstinline|?0|} can be solved with {\lstinline|+-right-identity|} with a ``twist''.
The symmetry of equality {\lstinline|sym|} enables us to swap both sides of an equation.

\begin{lstlisting}
+-comm zero    n = sym (+-right-identity n)
\end{lstlisting}

However, it is not obvious how to solve {\lstinline|?1|} straight out.
The proof has to be ``broken'', or ``split'' into two steps:

\begin{enumerate}
    \item Apply {\lstinline|+-suc|} with {\lstinline|sym|} to the right-hand side
    of the equation to get {\lstinline|suc (m + n) ≡ suc (n + m)|}.
    \item Apply the induction hypothesis to {\lstinline|cong suc|}.
\end{enumerate}

These small pieces of proofs are glued back together with the transitivity of
equality {\lstinline|trans|}.

\begin{lstlisting}
+-comm (suc m) n = trans (cong suc (+-comm m n)) (sym (+-suc n m))
\end{lstlisting}

\section{Equational Reasoning}

We can see that proofs are composable just like programs.

\begin{lstlisting}
trans (cong suc (+-comm m n)) (sym (+-suc n m))
\end{lstlisting}

However, it is difficult to see what is going on in between these clauses, and it could
get only worse as propositions get more complicated.
Imagine having dozens of {\lstinline|trans|}, {\lstinline|sym|} and {\lstinline|cong|}
spreading everywhere.

Fortunately, these complex proofs can be written in a concise and modular manner
with a simple yet powerful technique called \textit{equational reasoning}.
Agda's flexible mixfix syntax allows the technique to be implemented with just
a few combinators\cite{erikhesselinkpaulvisschers2008}.

This is best illustrated by an example:

\begin{lstlisting}
+-comm : ∀ m n → m + n ≡ n + m
+-comm zero    n = sym (+-right-identity n)
+-comm (suc m) n =
    begin
        suc m + n
    ≡⟨ refl ⟩
        suc (m + n)
    ≡⟨ cong suc (+-comm m n) ⟩
        suc (n + m)
    ≡⟨ sym (+-suc n m) ⟩
        n + suc m
    ∎
\end{lstlisting}

With equational reasoning, we can see how an expression equates to another,
step by step, justified by theorems. The first and the last steps correspond to
two sides of the equation of a proposition. {\lstinline|begin_|} marks the beginning
of a reasoning; {\lstinline|_≡⟨_⟩_|} chains two expressions with the justification
placed in between; {\lstinline|_∎|} marks the end of a reasoning (\textit{QED}).

\subsection{Anatomy of Equational Reasoning}

A typical equational reasoning can often be broken down into \textbf{three} parts.

\begin{enumerate}
    \item\label{eq-part1}
        Starting from the left-hand side of the equation, through a series of steps, the expression will be ``arranged'' into a form that allows the induction
        hypothesis to be applied.
        In the following example of {\lstinline|+-comm|}, nothing needs to be arranged
        because these two expressions are computationally equal
        (the {\lstinline|refl|} can be omitted).

        \begin{lstlisting}
            begin
                suc m + n
            ≡⟨ refl ⟩
                suc (m + n)
        \end{lstlisting}
    \item\label{eq-part2}
        {\lstinline|m + n|} emerged as part of the proposition which enables us
        to apply the induction hypothesis.

        \begin{lstlisting}
                suc (m + n)
            ≡⟨ cong suc (+-comm m n) ⟩
                suc (n + m)
        \end{lstlisting}
    \item\label{eq-part3}
        After applying the induction hypothesis, the expression is then ``rearranged''
        into the right-hand side of the equation, hence completes the proof.

        \begin{lstlisting}
                suc (n + m)
            ≡⟨ sym (+-suc n m) ⟩
                n + suc m
            ∎
        \end{lstlisting}
\end{enumerate}

\paragraph{arranging expressions}

To arrange an expression into the shape that we desire as in part \ref{eq-part1}
and part \ref{eq-part3}, while remaining equal,
we need properties such as commutativity or associativity of some operator,
or distributive properties when there is more than one operator.

The operators we will be dealing with often comes with these properties.
Take addition and multiplication, for example, together they form a nice semiring
structure.

\paragraph{substituting equals for equals}

As what we have seen in \ref{eq-part2}, sometimes there is only a part of an
expression needs to be substituted.
Say, we have a proof {\lstinline|eq : X ≡ Y|}, and we want to substitute {\lstinline|X|}
for {\lstinline|Y|} in a more complex expression {\lstinline|a b (c X) d|}.
We could ask {\lstinline|cong|} to ``target'' the part to substitute by supplying a
function like this:
%
\begin{lstlisting}
λ w → a b (c w) d
\end{lstlisting}
%
which abstracts the part we want to substitute away, such that:

\begin{lstlisting}
cong (λ w → a b (c w) d) eq : a b (c X) d ≡ a b (c Y) d
\end{lstlisting}

\section{Decidable Equality on Natural Numbers}

Equality is decidable on natural numbers, which means that we can always tell
whether two numbers are equal, and explain the reason with a proof.

\begin{lstlisting}
_≟_ : Decidable {A = ℕ} _≡_
zero  ≟ zero   = yes refl
suc m ≟ suc n  with m ≟ n
suc m ≟ suc .m | yes refl = yes refl
suc m ≟ suc n  | no prf   =
    no (prf ∘ (λ p → subst (λ x → m ≡ pred x) p refl))
zero  ≟ suc n  = no λ()
suc m ≟ zero   = no λ()
\end{lstlisting}

Decidable functions are often used together with \textit{with-abstractions}.

\begin{lstlisting}
answer : ℕ → Bool
answer n with n ≟ 42
answer n | yes p = true
answer n | no ¬p = false
\end{lstlisting}
%
where {\lstinline|p : n ≡ 42|} and  {\lstinline|¬p : n ≢ 42|}.

\section{Preorder}

Aside from stating that two expressions are equal, a proposition can also assert
that one expression is ``less than or equal to'' than another, given a preorder.

A preorder is a binary relation that is \textit{reflexive} and \textit{transitive}.
One of the instances of preorder on natural numbers is the binary relation
{\lstinline|_≤_|} (less than or equal to.)

\begin{lstlisting}
data _≤_ : ℕ → ℕ → Set where
    z≤n : ∀ {n}                 → zero  ≤ n
    s≤s : ∀ {m n} (m≤n : m ≤ n) → suc m ≤ suc n
\end{lstlisting}

The following is a proof of $ 3 \leq 5 $:

\begin{lstlisting}
3≤5 : 3 ≤ 5
3≤5 = s≤s (s≤s (s≤s z≤n))
\end{lstlisting}

To prove $ 3 \leq 5 $, we need a proof of $ 2 \leq 4 $ for {\lstinline|s≤s|},
and so on, until it reaches zero where it ends with a {\lstinline|z≤n|}.

Here are some other binary relations that can be defined with {\lstinline|_≤_|}.

\begin{lstlisting}
_<_ : Rel ℕ Level.zero
m < n = suc m ≤ n

_≰_ : Rel ℕ Level.zero
a ≰ b = ¬ a ≤ b

_≥_ : Rel ℕ Level.zero
m ≥ n = n ≤ m
\end{lstlisting}

\section{Preorder reasoning}

Combinators for equational reasoning can be further generalized to support \textit{preorder reasoning}.
Preorders are \textit{reflexive} and \textit{transitive},
which means that expressions can be chained with a series of relations just as that of
equational reasoning.

Suppose we already have {\lstinline|m≤m+n : ∀ m n → m ≤ m + n|} and we want to
prove a slightly different theorem.

\begin{lstlisting}
m≤n+m : ∀ m n → m ≤ n + m
m≤n+m m n =
    start
        m
    ≤⟨ m≤m+n m n ⟩
        m + n
    ≈⟨ +-comm m n ⟩
        n + m
    □
\end{lstlisting}
%
where {\lstinline|_≤⟨_⟩_|} and {\lstinline|_≈⟨_⟩_|} are respectively transitive
and reflexive combinators.\footnote{Combinators for preorder reasoning are
renamed to prevent conflictions with equational reasoning.}
Step by step, starting from the left-hand side of the relation, expressions get
greater and greater as it reaches the right-hand side the relation.

\paragraph{monotonicity of operators}

In equational reasoning, we could substitute part of an expression with something
equal with {\lstinline|cong|} because {\lstinline|_≡_|} is congruent.
However, we cannot substitute part of an expression with something \textit{greater}
in general.

Take the following function {\lstinline|f|} as example.

\begin{lstlisting}
f : ℕ → ℕ
f 0 = 1
f 1 = 0
f _ = 1
\end{lstlisting}

{\lstinline|f|} returns $ 1 $ on all inputs except for $ 1 $.
{\lstinline|0 ≤ 1|} holds, but it does not imply that {\lstinline|f 0 ≤ f 1|} also holds.
As a result, a generic mechanism like {\lstinline|cong|} does not exist in preorder reasoning.
Given \lstinline|x ≤ y|, we can substitute \lstinline|f x| by \lstinline|f y|
using a proof that \lstinline|f| is monotonic.
\footnote{Such a proof or theorem often goes by the name of \lstinline|f-mono|}.

\section{Decidable Preorder on Natural Numbers}

Preorder is also decidable on natural numbers, which means that we can always tell
whether one number is less than or equal to another.

\begin{lstlisting}
_≤?_ : Decidable _≤_
zero  ≤? _     = yes z≤n
suc m ≤? zero  = no λ()
suc m ≤? suc n with m ≤? n
...            | yes m≤n = yes (s≤s m≤n)
...            | no  m≰n = no  (m≰n ∘ ≤-pred)
\end{lstlisting}

With with-abstractions we could define some function like this:

\begin{lstlisting}
threshold : ℕ → ℕ
threshold n with n ≤? 87
threshold n | yes p = n
threshold n | no ¬p = 87
\end{lstlisting}
%
where {\lstinline|p : n ≤ 42|} and {\lstinline|¬p : n ≰ 42|}.

% Suppose we want to prove that {\lstinline|_+_|} is monotonic in both arguments.
%
% \begin{lstlisting}
% +-mono : ∀ m₁ m₂ n₁ n₂ → m₁ ≤ m₂ → n₁ ≤ n₂ → m₁ + n₁ ≤ m₂ + n₂
% \end{lstlisting}
%
% By induction on the argument of type {\lstinline|m₁ ≤ m₂|}, we get two goals.
% The type and context of the first goal is as follows:
%
% \begin{lstlisting}
% +-mono 0 m₂ n₁ n₂ z≤n n₁≤n₂ = ?0
%
% Goal of ?0 : n₁ ≤ m₂ + n₂
% n₁≤n₂      : n₁ ≤ n₂
% n₁, n₂, m₂ : ℕ
% \end{lstlisting}
%
% \begin{lstlisting}
% +-mono (suc m₁) (suc m₂) n₁ n₂ (s≤s p) n₁≤n₂ = ?1
% \end{lstlisting}
%
% The induction forces {\lstinline|m₁ m₂|} to change as well.
%
% \begin{lstlisting}
% +-mono : ∀ m₁ m₂ n₁ n₂ → m₁ ≤ m₂ → n₁ ≤ n₂ → m₁ + n₁ ≤ m₂ + n₂
% +-mono 0        m₂       n₁ n₂ z≤n     n₁≤n₂ = ?0
% +-mono (suc m₁) (suc m₂) n₁ n₂ (s≤s p) n₁≤n₂ = ?1
% \end{lstlisting}

% \begin{lstlisting}
% +-mono : ∀ m₁ m₂ n₁ n₂ → m₁ ≤ m₂ → n₁ ≤ n₂ → m₁ + n₁ ≤ m₂ + n₂
% +-mono 0        m₂       n₁ n₂ z≤n     n₁≤n₂ = ?0
% +-mono (suc m₁) (suc m₂) n₁ n₂ (s≤s p) n₁≤n₂ = ?1
% \end{lstlisting}

% Take \textit{monus} \footnote{Monus, or \textit{truncated subtraction}, is a
% kind of subtraction that never goes negative when the subtrahend is greater then
% the minued.} {\lstinline|_∸_|} for example:
%
% \begin{lstlisting}
% f : ∀ m n
%     → (prop : m ≤ n)
%     → m ∸ m ≤ m ∸ n
% f m n prop =
%     start
%         m ∸ m
%     ≤⟨ ? ⟩
%         m ∸ n
%     □
% \end{lstlisting}
%
% We cannot prove {\lstinline|f|} because {\lstinline|m ≤ n|} does not imply
% {\lstinline|m ∸ m ≤ m ∸ n|}\footnote{the second argument of {\lstinline|_∸_|}
% is \textit{contravariant} in the sense that the result of {\lstinline|_∸_|} would
% increase when the second argument decreases.}

% The proposition {\lstinline|f|} as seen above can only be disapproved, because
% the second argument of {\lstinline|_∸_|} is \textit{contravariant} in the sense
% that the result of {\lstinline|_∸_|} would increase when the second argument decreases.

% Even worse, the function that takes the substitute as an argument may not
% even be \textit{monotonic} like monus. As a result, a generic mechanism like
% {\lstinline|cong|} does not exist in preorder reasoning. We can only substitute
% part of an expression when the function is \textit{monotonic}. The monotonicity
% of each function are different and have to be proved


%
% Similar to that of equation reasoning, sometimes we need to rewrite expressions with
% {\lstinline|_≈⟨_⟩_|} when they are equal to each other (An equivalence relation is also a
% preorder). We would also skip these steps.
%
% Some of the obvious transitive steps (that is written in {\lstinline|_≤⟨_⟩_|})
% will also be skipped.

\section{Skipping trivial proofs}

From now on, we will dispense with most of the steps and justifications in
equational and preorder reasonings,
because it is often obvious to see what happened in the process.

In fact, there are is no formal distinction between the proofs we disregard and
those we feel important. They are all equally indispensable to Agda.

\section{Relevant Properties of Natural Numbers}

Relevant properties of {\lstinline|ℕ|} used in the remainder of the thesis
are introduced in this section.

Aside from some basic properties taken from the standard library, we have
also added some useful theorems, lemmata, and corollaries.
\footnote{\textit{Theorem}, \textit{lemma}, \textit{corollary} and \textit{property}
are all synonyms for \textit{established proposition}. There are no formal
distinction between these terms and they are used exchangeably in the thesis.}

\subsection{Equational Propositions}

\subsubsection{natural number}

\begin{lstlisting}
data ℕ : Set where
    zero : ℕ
    suc : ℕ → ℕ
\end{lstlisting}

\begin{itemize}
    \item {\lstinline|cancel-suc : ∀ {x y} → suc x ≡ suc y → x ≡ y|}
        \\ suc is injective.
\end{itemize}

\subsubsection{addition}

\begin{lstlisting}
_+_ : ℕ → ℕ → ℕ
zero  + y = y
suc x + y = suc (x + y)
\end{lstlisting}

\begin{itemize}
    \item {\lstinline|+-right-identity : ∀ n → n + 0 ≡ n|}
        \\ the right identity of addition.
    \item {\lstinline|+-suc : ∀ m n → m + suc n ≡ suc (m + n)|}
        \\ moving {\lstinline|suc|} from one term to another.
    \item {\lstinline|+-assoc : ∀ m n o → (m + n) + o ≡ m + (n + o)|}
        \\ the associative property of addition.
    \item {\lstinline|+-comm : ∀ m n → m + n ≡ n + m|}
        \\ the commutative property of addition.
    \item {\lstinline|[a+b]+c≡[a+c]+b : ∀ a b c → a + b + c ≡ a + c + b|}
        \\ a convenient corollary for swapping terms.
    \item {\lstinline|a+[b+c]≡b+[a+c] : ∀ a b c → a + (b + c) ≡ b + (a + c)|}
        \\ a convenient corollary for swapping terms.
    \item {\lstinline|cancel-+-left : ∀ i {j k} → i + j ≡ i + k → j ≡ k|}
        \\ the left cancellation property of addition.
    \item {\lstinline|cancel-+-right : ∀ k {i j} → i + k ≡ j + k → i ≡ j|}
        \\ the right cancellation property of addition.
\end{itemize}

\subsubsection{multiplication}

\begin{lstlisting}
_*_ : ℕ → ℕ → ℕ
zero  * y = y
suc x * y = y + (x * y)
\end{lstlisting}

\begin{itemize}
    \item {\lstinline|*-right-zero : ∀ n → n * 0 ≡ 0|}
        \\ the right absorbing element of multiplication.
    \item {\lstinline|*-left-identity : ∀ n → 1 * n ≡ n|}
        \\ the left identity of addition multiplication.
    \item {\lstinline|*-right-identity : ∀ n → n * 1 ≡ n|}
        \\ the right identity of addition multiplication.
    \item {\lstinline|+-*-suc : ∀ m n → m * suc n ≡ m + m * n|}
        \\ multiplication over {\lstinline|suc|}.
    \item {\lstinline|*-assoc : ∀ m n o → (m * n) * o ≡ m * (n * o)|}
        \\ the associative property of multiplication.
    \item {\lstinline|*-comm : ∀ m n → m * n ≡ n * m|}
        \\ the commutative property of multiplication.
    \item {\lstinline|distribʳ-*-+ : ∀ m n o → (n + o) * m ≡ n * m + o * m|}
        \\ the right distributive property of multiplication over addition.
    \item {\lstinline|distrib-left-*-+ : ∀ m n o → m * (n + o) ≡ m * n + m * o|}
        \\ the left distributive property of multiplication over addition.
\end{itemize}

\subsubsection{monus}

Monus, or \textit{truncated subtraction}, is a kind of subtraction that never
goes negative when the subtrahend is greater than the minued.

\begin{lstlisting}
_∸_ : Nat → Nat → Nat
n     ∸ zero = n
zero  ∸ suc m = zero
suc n ∸ suc m = n ∸ m
\end{lstlisting}

\begin{itemize}
    \item {\lstinline|0∸n≡0 : ∀ n → 0 ∸ n ≡ 0|}
    \item {\lstinline|n∸n≡0 : ∀ n → n ∸ n ≡ 0|}
    \item {\lstinline|m+n∸n≡m : ∀ m n → (m + n) ∸ n ≡ m|}
    \item {\lstinline|m+n∸m≡n : ∀ {m n} → m ≤ n → m + (n ∸ m) ≡ n|}
    \item {\lstinline|m∸n+n≡m : ∀ {m n} → n ≤ m → m ∸ n + n ≡ m|}
    \item {\lstinline|∸-+-assoc : ∀ m n o → (m ∸ n) ∸ o ≡ m ∸ (n + o)|}
        \\ the associative property of monus and addition.
    \item {\lstinline|+-∸-assoc : ∀ m {n o} → o ≤ n → (m + n) ∸ o ≡ m + (n ∸ o)|}
        \\ the associative property of monus and addition.
    \item {\lstinline|*-distrib-∸ʳ : ∀ m n o → (n ∸ o) * m ≡ n * m ∸ o * m|}
        \\ the right distributive property of monus over multiplication.
\end{itemize}

\subsubsection{min and max}

So called {\lstinline|min|} and {\lstinline|max|} in Haskell.
Min {\lstinline|_⊓_|} computes the lesser of two numbers.

\begin{lstlisting}
_⊓_ : ℕ → ℕ → ℕ
zero  ⊓ n     = zero
suc m ⊓ zero  = zero
suc m ⊓ suc n = suc (m ⊓ n)
\end{lstlisting}

Max {\lstinline|_⊔_|} computes the greater of two numbers.

\begin{lstlisting}
_⊔_ : ℕ → ℕ → ℕ
zero  ⊔ n     = n
suc m ⊔ zero  = suc m
suc m ⊔ suc n = suc (m ⊔ n)
\end{lstlisting}

\begin{itemize}
    \item {\lstinline|⊓-comm : ∀ m n → m ⊓ n ≡ n ⊓ m|}
        \\ the commutative property of min.
    \item {\lstinline|⊔-comm : ∀ m n → m ⊔ n ≡ n ⊔ m|}
        \\ the commutative property of max.
\end{itemize}

\subsection{Relational Propositions}

\subsubsection{natural number}

\begin{itemize}
    \item {\lstinline|≤-pred : ∀ {m n} → suc m ≤ suc n → m ≤ n|}
        \\ inverse of {\lstinline|s≤s|}.
    \item {\lstinline|<⇒≤ : _<_ ⇒ _≤_|}
    \item {\lstinline|>⇒≰ : _>_ ⇒ _≰_|}
    \item {\lstinline|≤⇒≯ : _≤_ ⇒ _≯_|}
    \item {\lstinline|<⇒≱ : _<_ ⇒ _≱_|}
    \item {\lstinline|>⇒≢ : _>_ ⇒ _≢_|}
    \item {\lstinline|≥⇒≮ : _≥_ ⇒ _≮_|}
    \item {\lstinline|<⇒≢ : _<_ ⇒ _≢_|}
    \item {\lstinline|≤∧≢⇒< : ∀ {m n} → m ≤ n → m ≢ n → m < n|}
    \item {\lstinline|≥∧≢⇒> : ∀ {m n} → m ≥ n → m ≢ n → m > n|}
\end{itemize}

\subsubsection{addition}

\begin{itemize}
    \item {\lstinline|≤-step : ∀ {m n} → m ≤ n → m ≤ 1 + n|}
    \item {\lstinline|≤-steps : ∀ {m n} k → m ≤ n → m ≤ k + n|}
    \item {\lstinline|m≤m+n : ∀ m n → m ≤ m + n|}
    \item {\lstinline|n≤m+n : ∀ m n → n ≤ m + n|}
    \item {\lstinline|_+-mono_ : ∀ {m₁ m₂ n₁ n₂} → m₁ ≤ m₂ → n₁ ≤ n₂ → m₁ + n₁ ≤ m₂ + n₂|}
        \\ the monotonicity of addition
    \item {\lstinline|n+-mono : ∀ {i j} n → i ≤ j → n + i ≤ n + j|}
        \\ {\lstinline|_+-mono_|} with the first argument fixed.
    \item {\lstinline|+n-mono : ∀ {i j} n → i ≤ j → n + i ≤ n + j|}
        \\ {\lstinline|_+-mono_|} with the second argument fixed.
    \item {\lstinline|n+-mono-inverse : ∀ n → ∀ {a b} → n + a ≤ n + b → a ≤ b|}
        \\ the inverse of {\lstinline|n+-mono|}
    \item {\lstinline|+n-mono-inverse : ∀ n → ∀ {a b} → a + n ≤ b + n → a ≤ b|}
        \\ the inverse of {\lstinline|+n-mono|}
    \item {\lstinline|+-mono-contra : ∀ {a b c d} → a ≥ b → a + c < b + d → c < d|}
\end{itemize}

\subsubsection{monus}

\begin{itemize}
    \item {\lstinline|n∸m≤n : ∀ m n → n ∸ m ≤ n|}
    \item {\lstinline|n≤m+n∸m : ∀ m n → n ≤ m + (n ∸ m)|}
    \item {\lstinline|∸-mono : ∀ x y u v → x ≤ y → u ≥ v → x ∸ u ≤ y ∸ v|}
        \\ the monotonicity of monus
\end{itemize}

\end{document}
